apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId "org.schabi.newpipe"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 68
        versionName "0.14.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled true

            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

ext {
    supportLibVersion = '27.1.1'
    exoPlayerLibVersion = '2.8.2'
    roomDbLibVersion = '1.1.1'
    leakCanaryLibVersion = '1.5.4'
    okHttpLibVersion = '3.10.0'
    icepickLibVersion = '3.2.0'
    stethoLibVersion = '1.5.0'
}

dependencies {
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude module: 'support-annotations'
    }

    implementation 'com.github.yausername:NewPipeExtractor:d1ff1c7'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.8.9'

    implementation "com.android.support:appcompat-v7:$supportLibVersion"
    implementation "com.android.support:support-v4:$supportLibVersion"
    implementation "com.android.support:design:$supportLibVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibVersion"
    implementation "com.android.support:preference-v14:$supportLibVersion"

    implementation 'ch.acra:acra:4.9.2'

    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'com.github.nirhart:ParallaxScroll:dd53d1f9d1'
    implementation 'com.nononsenseapps:filepicker:4.2.1'

    implementation "com.google.android.exoplayer:exoplayer:$exoPlayerLibVersion"
    implementation "com.google.android.exoplayer:extension-mediasession:$exoPlayerLibVersion"

    debugImplementation "com.facebook.stetho:stetho:$stethoLibVersion"
    debugImplementation "com.facebook.stetho:stetho-urlconnection:$stethoLibVersion"
    debugImplementation 'com.android.support:multidex:1.0.3'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.14'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

    implementation "android.arch.persistence.room:runtime:$roomDbLibVersion"
    implementation "android.arch.persistence.room:rxjava2:$roomDbLibVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomDbLibVersion"

    implementation "frankiesardo:icepick:$icepickLibVersion"
    annotationProcessor "frankiesardo:icepick-processor:$icepickLibVersion"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryLibVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryLibVersion"


    implementation "com.squareup.okhttp3:okhttp:$okHttpLibVersion"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:$stethoLibVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:cardview-v7:27.1.1'
}
